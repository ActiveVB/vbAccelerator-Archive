<html lang="en" >

<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/vbMedia/DIB_Sections/Alpha_DIBSection/VB6_Alpha_DIB_Section_Demonstration_zip_frmAlphaTest_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:47 GMT -->
<head>
<title>vbAccelerator - Contents of code file: frmAlphaTest.frm</title><meta http-equiv="Content-Type" content="text/html;charset=utf-8" /><meta name="AUTHOR" content="Steve McMahon" /><meta name="KEYWORDS" content=".NET, .NET Framework, C#, CSharp, VB, Visual Basic, VB5, VB6, Visual Basic, Active X, Controls, Components" /><meta name="DESCRIPTION" content="vbAccelerator - Contents of code file: frmAlphaTest.frm" /><link rel="stylesheet" href="../../../../../res/screen.css" media="SCREEN" /><link rel="stylesheet" href="../../../../../res/print.css" media="PRINT" /></head><body>
<table>
<tr class="adbar" bgcolor="#83726a"><tr class="footer"><td></td>
<td>
<p class="center">

<script type="text/javascript"><!--
google_ad_client = "pub-4690375644913255";
google_ad_width = 728;
google_ad_height = 90;
google_ad_format = "728x90_as";
google_ad_channel ="";
google_ad_type = "text_image";
google_color_border = "336699";
google_color_bg = "FFFFFF";
google_color_link = "0000FF";
google_color_url = "008000.html";
google_color_text = "000000";
//--></script>
<script type="text/javascript"
  src="../../../../../../../pagead2.googlesyndication.com/pagead/f.txt">
</script>
</p>
</td>
<td></td>
</tr></tr><tr class="navbar"><td><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">vbMedia</a>&#160;.&#160;<a href="../index.html">DIB Sections</a>&#160;.&#160;<a href="article.html">Alpha DIBSections</a>&#160;.&#160;<a href="VB6_Alpha_DIB_Section_Demonstration.html">VB6 Alpha DIB Section Demonstration</a>&#160;.&#160;frmAlphaTest.frm</p></td><td></td></tr><tr class="navbar"><td colspan="3"><hr /></td></tr><tr valign="top"><td class="sidebar"><img src="../../../../../res/download.png" width="125" height="21" alt="Downloads" /><p class="nav"><a href="Simple_Alpha_Sample.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />Simple Alpha Sample</a> (26K)</p><p /><p class="nav"><a href="VB5_Alpha_DIB_Section_Demonstration.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB5 Alpha DIB Section Demonstration</a> (101K)</p><p /><p class="nav"><a href="VB6_Alpha_DIB_Section_Demonstration.html"><img src="../../../../../res/get.png" width="8" height="8" alt="Download Page" />VB6 Alpha DIB Section Demonstration</a> (97K)</p><br /><br /><img src="../../../../../res/information.png" width="125" height="21" alt="Information" /><p class="nav">Zip:3056</p><p class="nav">&#160;&#160;<a href="http://www.vbaccelerator.com/linkto.asp?id=3056&amp;type=Zip&amp;title=VB6%20Alpha%20DIB%20Section%20Demonstration%2Ezip%5FfrmAlphaTest">Link to code Zip</a></p><p class="nav">Applies To:</p><p class="nav">&#160;&#160;VB6</p><p class="nav">Author:</p><p class="nav">&#160;&#160;<a href="mailto:steve@vbaccelerator.com">Steve McMahon</a></p><br /><br /><img src="../../../../../res/bugTrak.png" width="125" height="21" alt="BugTrak System" /><p class="nav">No logged bugs.</p><br /><br /><img src="../../../../../res/updates.png" width="125" height="21" alt="Updates" /><p class="nav"><img src="../../../../../res/update.png" width="8" height="8" alt="Update" />13 Jan 2003<br />First Posted</p><br /><br /><img src="../../../../../res/related.png" width="125" height="21" alt="Related Items" />﻿<p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=2946">Window Shapes Using Layering</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=3015">256 Colour DIBSections </a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=17">Subclassing Without The Crashes</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=3017">True Colour DIBSection</a></p><p class="nav"><img src="../../../../../res/rel.png" width="8" height="8" alt="Related Item" /><a href="http://www.vbaccelerator.com/article.asp?id=1387">Replace one Colour with another in a Picture using BitBlt</a></p><br /><br /><img src="../../../../../res/search.png" width="125" height="21" alt="Search" />
<!-- Search Google -->
<form method="GET" action="http://www.google.com/custom">
<a href="http://www.google.com/search"><img src="../../../../../../../www.google.com/logos/Logo_40wht.gif" alt="Google" /></A><br />
<input type="text" name="q" size="31" maxlength="255" value="" class="search" /><br />
<input type="submit" name="sa" value="Search Google" class="search" />
<input type="hidden" name="cof" value="T:black;LW:330;ALC:990000;L:http://www.vbaccelerator.com/images/accl9.gif;LC:660000;LH:66;BGC:white;AH:left;VLC:999966;S:http://vbaccelerator.com/;GALT:999966;AWFID:67c7759ff27e5395;" />
<input type="hidden" name="domains" value="vbaccelerator.com" /><br />
<p class="nav"><input type="radio" name="sitesearch" value="" />Search WWW<br /></p>
<p class="nav"><input type="radio" name="sitesearch" value="vbaccelerator.com" checked="true" />vbAccelerator<br /></p>
</FORM>
<!-- Search Google -->
<br /><br />

<a href="http://www.vbaccelerator.com/home/The_Site/NewSite/article.asp"><img src="../../../../../res/newsite.png" width="125" height="129" alt="The New Site" /></a>
<br /><br />
</td><td width="100%" valign="top"><h1>vbAccelerator - Contents of code file: frmAlphaTest.frm</h1><pre>VERSION 5.00
Begin VB.Form frmAlphaTest 
   AutoRedraw      =   -1  'True
   Caption         =   "Alpha DIB Section Tester"
   ClientHeight    =   7080
   ClientLeft      =   60
   ClientTop       =   450
   ClientWidth     =   7350
   BeginProperty Font 
      Name            =   "Tahoma"
      Size            =   8.25
      Charset         =   0
      Weight          =   400
      Underline       =   0   'False
      Italic          =   0   'False
      Strikethrough   =   0   'False
   EndProperty
   Icon            =   "frmAlphaTest.frx":0000
   LinkTopic       =   "Form1"
   ScaleHeight     =   472
   ScaleMode       =   3  'Pixel
   ScaleWidth      =   490
   StartUpPosition =   3  'Windows Default
   Begin VB.CheckBox chkColourised 
      Caption         =   "&amp;Colourise"
      Height          =   255
      Left            =   5040
      TabIndex        =   3
      Top             =   2160
      Width           =   1635
   End
   Begin VB.CheckBox chkEnabled 
      Caption         =   "&amp;Enabled"
      Height          =   255
      Left            =   5040
      TabIndex        =   2
      Top             =   1860
      Value           =   1  'Checked
      Width           =   1635
   End
   Begin VB.PictureBox picAlphaButton 
      AutoRedraw      =   -1  'True
      BorderStyle     =   0  'None
      Height          =   1335
      Left            =   5100
      ScaleHeight     =   89
      ScaleMode       =   3  'Pixel
      ScaleWidth      =   93
      TabIndex        =   1
      ToolTipText     =   "Click for vbAccelerator.com"
      Top             =   420
      Width           =   1395
   End
   Begin VB.CommandButton cmdSave 
      Caption         =   "&amp;Save..."
      Height          =   435
      Left            =   1320
      TabIndex        =   0
      Top             =   6420
      Width           =   1335
   End
   Begin VB.Label lblAlphaButton 
      Caption         =   "Alpha Button Demonstration:"
      Height          =   255
      Left            =   5040
      TabIndex        =   8
      Top             =   120
      Width           =   2175
   End
   Begin VB.Label lblFaded 
      Caption         =   "Faded Alpha Blend:"
      Height          =   555
      Left            =   120
      TabIndex        =   7
      Top             =   1620
      Width           =   975
   End
   Begin VB.Label lblFullAlpha 
      Caption         =   "Normal Alpha Blend:"
      Height          =   555
      Left            =   120
      TabIndex        =   6
      Top             =   960
      Width           =   975
   End
   Begin VB.Label lblMask 
      Caption         =   "Alpha Channel:"
      Height          =   435
      Left            =   120
      TabIndex        =   5
      Top             =   540
      Width           =   975
   End
   Begin VB.Label lblImage 
      Caption         =   "Image:"
      Height          =   315
      Left            =   120
      TabIndex        =   4
      Top             =   120
      Width           =   975
   End
End
Attribute VB_Name = "frmAlphaTest"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Explicit

Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" ( _
    lpvDest As Any, lpvSource As Any, ByVal cbCopy As Long)

Private Type SAFEARRAYBOUND
    cElements As Long
    lLbound As Long
End Type
Private Type SAFEARRAY2D
    cDims As Integer
    fFeatures As Integer
    cbElements As Long
    cLocks As Long
    pvData As Long
    Bounds(0 To 1) As SAFEARRAYBOUND
End Type
Private Declare Function VarPtrArray Lib "msvbvm60.dll" Alias "VarPtr" (Ptr()
 As Any) As Long

Private m_cMask As New cAlphaDibSection
Private m_cImage As New cAlphaDibSection
Private m_cAlphaImage As New cAlphaDibSection

Private WithEvents m_cAlphaButton As cAlphaButton
Attribute m_cAlphaButton.VB_VarHelpID = -1

Private Sub createAlphaImage()
   
   ' Load picture:
   m_cImage.CreateFromPicture LoadPicture(App.Path &amp; "\logo.bmp")
   
   ' Load alpha channel:
   m_cMask.CreateFromPicture LoadPicture(App.Path &amp; "\logomask.bmp")
   
   ' Create a new image, which is just a copy of the picture
   ' in m_cImage to build the alpha version in.  Note if
   ' we didn't want to display the image without alpha later,
   ' we could just work on m_cImage directly instead.
   m_cAlphaImage.Create m_cImage.Width, m_cImage.Height
   m_cImage.PaintPicture m_cAlphaImage.hdc
   
   ' Point byte arrays at the image bits for ease of
   ' manipulation of the data:
   Dim tMask As SAFEARRAY2D
   Dim bMask() As Byte
   Dim tImage As SAFEARRAY2D
   Dim bImage() As Byte
    
    With tMask
        .cbElements = 1
        .cDims = 2
        .Bounds(0).lLbound = 0
        .Bounds(0).cElements = m_cMask.Height
        .Bounds(1).lLbound = 0
        .Bounds(1).cElements = m_cMask.BytesPerScanLine()
        .pvData = m_cMask.DIBSectionBitsPtr
    End With
    CopyMemory ByVal VarPtrArray(bMask()), VarPtr(tMask), 4
    
    With tImage
        .cbElements = 1
        .cDims = 2
        .Bounds(0).lLbound = 0
        .Bounds(0).cElements = m_cAlphaImage.Height
        .Bounds(1).lLbound = 0
        .Bounds(1).cElements = m_cAlphaImage.BytesPerScanLine()
        .pvData = m_cAlphaImage.DIBSectionBitsPtr
    End With
    CopyMemory ByVal VarPtrArray(bImage()), VarPtr(tImage), 4
   
   Dim x As Long, y As Long
   Dim bAlpha As Long
   For y = 0 To m_cAlphaImage.Height - 1
      For x = 0 To m_cAlphaImage.BytesPerScanLine - 4 Step 4 ' each item has 4
       bytes: R,G,B,A
         ' Get the red value from the mask to use as the alpha
         ' value:
         bAlpha = bMask(x, y)
         ' Set the alpha in the alpha image..
         bImage(x + 3, y) = bAlpha
         ' Now premultiply the r/g/b values by the alpha divided
         ' by 255.  This is required for the AlphaBlend GDI function,
         ' see MSDN/Platform SDK/GDI/BLENDFUNCTION for more
         ' details:
         bImage(x, y) = bImage(x, y) * bAlpha \ 255
         bImage(x + 1, y) = bImage(x + 1, y) * bAlpha \ 255
         bImage(x + 2, y) = bImage(x + 2, y) * bAlpha \ 255
      Next x
   Next y
   
   ' Clear up the temporary array descriptors.  You
   ' only need to do this on NT but best to be safe.
   CopyMemory ByVal VarPtrArray(bMask), 0&amp;, 4
   CopyMemory ByVal VarPtrArray(bImage), 0&amp;, 4

   lblImage.Move 2, 2
   lblMask.Move 2, 4 + m_cAlphaImage.Height
   lblFullAlpha.Move 2, 6 + m_cAlphaImage.Height * 2
   lblFaded.Move 2, 8 + m_cAlphaImage.Height * 3

   Draw
   
End Sub

Private Sub createAlphaButton()
   Set m_cAlphaButton = New cAlphaButton
   With m_cAlphaButton
      Set .DrawObject = picAlphaButton
      If InDebug Then
         .AlphaLogoFileName = App.Path &amp; "\res\alphalogo.bmp"
         .AlphaLogoDownFileName = App.Path &amp; "\res\alphalogodown.bmp"
      Else
         .AlphaLogoId = 1
         .AlphaLogoDownId = 2
         .hInstance = App.hInstance
      End If
      .LoadImages
      picAlphaButton.Width = .Width
      picAlphaButton.Height = .Height
      .Draw
   End With
End Sub

Private Sub Draw()
   
   Dim x As Long
   Dim y As Long
   x = lblImage.Left + lblImage.Width
   
   ' Draw the original image:
   m_cImage.PaintPicture Me.hdc, x, 2
   ' Draw the mask:
   m_cMask.PaintPicture Me.hdc, x, m_cMask.Height + 4
   
   ' Draw the alpha version against various
   ' backgrounds and with differing constant alpha:
   y = (m_cAlphaImage.Height + 2) * 2
   Dim i As Long, lCol As Long
   For i = 1 To 4
      Select Case i
      Case 1
         lCol = vbButtonFace
      Case 2
         lCol = vbWindowBackground
      Case 3
         lCol = vb3DShadow
      Case 4
         lCol = RGB(108, 182, 255)
      End Select
      
      ' Draw backgrounds:
      Me.Line (x, y)-(x + m_cAlphaImage.Width, y + m_cAlphaImage.Height), lCol,
       BF
      Me.Line (x, y + m_cAlphaImage.Height + 2)-(x + m_cAlphaImage.Width, y +
       m_cAlphaImage.Height * 2 + 2), lCol, BF
      If (i = 3) Then
         Me.Line (x + m_cAlphaImage.Width \ 2, y + m_cAlphaImage.Height \ 2)-(x
          + m_cAlphaImage.Width, y + m_cAlphaImage.Height * 3 \ 2), RGB(255, 0,
          0), BF
      ElseIf (i = 4) Then
         Me.Line (x, y + m_cAlphaImage.Height \ 2)-(x + m_cAlphaImage.Width \
          2, y + m_cAlphaImage.Height * 3 \ 2), RGB(255, 0, 0), BF
      End If
      
      ' Draw normally, so only the drop shadow uses the alpha effect:
      m_cAlphaImage.AlphaPaintPicture Me.hdc, x, y
      
      ' Apply a constant alpha of 64 so all parts of the image, including the
      ' drop shadow, are ghosted:
      m_cAlphaImage.AlphaPaintPicture Me.hdc, x, y + m_cAlphaImage.Height + 2,
       lConstantAlpha:=64
      
      x = x + m_cAlphaImage.Width + 2
   Next i
   
   Me.Refresh
End Sub

Private Sub chkColourised_Click()
   If (chkColourised.value = vbChecked) Then
      m_cAlphaButton.Colourise 0.2
   Else
      m_cAlphaButton.Colourise 3.5
   End If
End Sub

Private Sub chkEnabled_Click()
   m_cAlphaButton.Enabled = (chkEnabled.value = vbChecked)
End Sub

Private Sub cmdSave_Click()
Dim sI As String
   sI = InputBox("Please enter the name to save the alpha image to:",
    App.Title, App.Path &amp; "\alphalogo.bmp")
   If (Len(sI) &gt; 0) Then
      m_cAlphaImage.SavePicture sI
   End If
End Sub

Private Sub Form_Load()
   createAlphaButton
   createAlphaImage
End Sub


Private Sub Form_Resize()
   Draw
End Sub

Private Sub m_cAlphaButton_Click()
   MsgBox "Button Clicked", vbInformation
End Sub

Private Sub picAlphaButton_MouseDown(Button As Integer, Shift As Integer, x As
 Single, y As Single)
   m_cAlphaButton.MouseDown Button, Shift, x, y
End Sub

Private Sub picAlphaButton_MouseMove(Button As Integer, Shift As Integer, x As
 Single, y As Single)
   m_cAlphaButton.MouseMove Button, Shift, x, y
End Sub

Private Sub picAlphaButton_MouseUp(Button As Integer, Shift As Integer, x As
 Single, y As Single)
   m_cAlphaButton.MouseUp Button, Shift, x, y
End Sub

</pre><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p><p>&#160;</p></td><td class="rightbar" valign="top" bgcolor="#C2BEB1">
<!-- No right bar content currently defined -->
</td></tr><tr class="footer"><td></td>
<td>
<p class="center"></p>
</td>
<td></td>
</tr><tr><td colspan="3"><hr /></td></tr><tr class="footer"><td valign="top"><a href="http://www.vbaccelerator.com/home/index.asp"><img width="125" height="25" src="../../../../../res/vbaccelerator.png" alt="vbAccelerator Logo" /></a></td><td valign="bottom"><p class="nav"><a href="http://www.vbaccelerator.com/home/index.asp">Home</a>&#160;.&#160;<a href="../../../../index.html">VB</a>&#160;.&#160;<a href="../../../index.html">Code</a>&#160;.&#160;<a href="../../index.html">vbMedia</a>&#160;.&#160;<a href="../index.html">DIB Sections</a>&#160;.&#160;<a href="article.html">Alpha DIBSections</a>&#160;.&#160;<a href="VB6_Alpha_DIB_Section_Demonstration.html">VB6 Alpha DIB Section Demonstration</a>&#160;.&#160;frmAlphaTest.frm</p><br /><p class="nav"><a href="http://www.vbaccelerator.com/home/The_Site/Copyright/article.asp">Copyright</a> &#169; 2003 Steve McMahon <a href="mailto:steve@vbaccelerator.com">steve@vbaccelerator.com</a>.  All rights reserved.<br />Last Updated: 12 April 2003</p></td><td></td></tr></table>
</body>
<!-- Mirrored from www.vbaccelerator.com/home/VB/Code/vbMedia/DIB_Sections/Alpha_DIBSection/VB6_Alpha_DIB_Section_Demonstration_zip_frmAlphaTest_frm.asp by HTTrack Website Copier/3.x [XR&CO'2014], Fri, 19 Jun 2015 21:25:47 GMT -->
</html>